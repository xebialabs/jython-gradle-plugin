// First, apply the publishing plugin
buildscript {
  repositories {
    mavenCentral()
    ["releases", "public"].each { r ->
            maven {
                url "${nexusBaseUrl}/repositories/${r}"
                credentials {
                    username nexusUserName
                    password nexusPassword
                }
            }
        }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:1.3.1"
    classpath "com.xebialabs.gradle.plugins:license-gradle-plugin:0.17.0"
  }
}

plugins {
  id "com.gradle.plugin-publish" version "1.3.0"
  id "groovy"
  id "idea"
  id "java-gradle-plugin"
}

apply plugin: 'com.xebialabs.license'

group = 'com.xebialabs.gradle.plugins'
version = '0.12.0'
defaultTasks 'build'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenLocal()
    mavenCentral()
}

license {
  header = rootProject.file('LICENSE')
  strictCheck = true
  mapping {
    java = 'SLASHSTAR_STYLE'
  }
  exclude('com/hierynomus/protocol/commons/Base64.java')
  exclude('**/*.txt')
  exclude('**/*.properties')
}


idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencies {
  implementation gradleApi()
  implementation localGroovy()
  implementation 'org.apache.groovy:groovy-xml:4.0.27'
  implementation 'org.apache.httpcomponents.client5:httpclient5:5.4.1'
  implementation "org.apache.commons:commons-compress:1.27.1"

  testImplementation('org.spockframework:spock-core:2.3-groovy-4.0') {
    exclude group: 'org.codehaus.groovy', module: 'groovy-all'
  }
  testImplementation 'org.apache.groovy:groovy-xml:4.0.27'
  testImplementation 'com.xebialabs.restito:restito:1.1.2'
  testImplementation gradleTestKit()
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testRuntimeOnly 'org.apache.groovy:groovy-xml:4.0.27'
}

// This disables the pedantic doclint feature
tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.withType(ProcessResources).configureEach {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
    }
    afterSuite { descriptor, result ->
        def indicator = "\u001B[32m✓\u001b[0m"
        if (result.failedTestCount > 0) {
            indicator = "\u001B[31m✘\u001b[0m"
        }
        logger.lifecycle("$indicator Test ${descriptor.name}; Executed: ${result.testCount}/\u001B[32m${result.successfulTestCount}\u001B[0m/\u001B[31m${result.failedTestCount}\u001B[0m")
    }
}

gradlePlugin {
    website = "https://github.com/xebialabs/jython-gradle-plugin"
    vcsUrl = "https://github.com/xebialabs/jython-gradle-plugin.git"
    plugins {
        jythonPlugin {
            id = "com.xebialabs.jython"
            displayName = "Jython plugin for Gradle (Digital.ai fork)"
            description = "Bundle Jython/Python libraries in your JAR"
            tags.set(["jython", "python"])
            implementationClass = "com.hierynomus.gradle.plugins.jython.JythonPlugin"
        }
    }
}

publishing {
    repositories {
        maven {
            url = uri("${project.property("nexusBaseUrl")}/repositories/releases")
            credentials {
                username = project.property("nexusUserName").toString()
                password = project.property("nexusPassword").toString()
            }
        }
    }
}
